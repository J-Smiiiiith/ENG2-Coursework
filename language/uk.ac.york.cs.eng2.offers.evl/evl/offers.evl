// Write your EVL validation rules here.
//
// To run them, right-click on the Sirius diagram background and select "Validate diagram".

//Check product names are unique
context Product {

  constraint nameIsUnique {
    check: Product.all.select(p | p.name == self.name).size() == 1
    message: "Product name '" + self.name + "' is not unique."
  }
  
  critique nameStartsWithCapitalLetter {
    check: self.name == self.name.ftuc()
    message: "Product names should start with an upper case letter."
    
    fix {
      title: "Rename '" + self.name + "' to: " + self.name.ftuc()
      do {
        self.name = self.name.ftuc();
      }
    }
  }
}

//Check percentages are between 0 and 100
context OfferPriceReduction {
	
	constraint percentageBetween0And100 {
		check: self.percentageReduction > 0 and self.percentageReduction <= 100
		message: "Price reduction offer: '" + self.name + "' has percentage reduction: " 
		+ self.percentageReduction + ". This value must b between 0 and 100."
	}
}

//Check one rule triggered by no other rules
context OfferRule {

  constraint onlyOneStartRuleExists {
  
  	guard: Trigger.all.select(t | t.target == self).isEmpty()

    check: OfferRule.all.select(r | Trigger.all.select(t | t.target == r).isEmpty()).size() == 1

    message: "'" + self.name + "'" + "is not targetted by any trigger. There only be 1 OfferRule " +
    "that is not targeted by any Trigger."
  }
}
